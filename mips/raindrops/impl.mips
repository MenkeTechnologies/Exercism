.macro check (%n, %letter)
    rem $t1, $a0, %n
    bne $t1, 0, checked
        li $t1, 'P'
        sb $t1, 0($t0)
        li $t1, 'l'
        sb $t1, 1($t0)
        li $t1, %letter
        sb $t1, 2($t0)
        li $t1, 'n'
        sb $t1, 3($t0)
        li $t1, 'g'
        sb $t1, 4($t0)
        add $t0, $t0, 5
    checked:
.end_macro
.globl raindrops
raindrops:
    move $t0, $a1
    check (3, 'i')
    check (5, 'a')
    check (7, 'o')
    bne $a1, $t0, return
    move $t1, $a0
    start_log10:
        beq $t1, 0, end_log10
        div $t1, $t1, 10
        add $t0, $t0, 1
        j start_log10
    end_log10:
    move $t1, $a0
    start_itoa:
        beq $t1, 0, end_itoa
        sub $t0, $t0, 1
        li $t2, 10
        div $t1, $t2
        mfhi $t2
        add $t2, $t2, '0'
        sb $t2, 0($t0)
        mflo $t1
        j start_itoa
    end_itoa:
    return:
    jr $ra
